000100 TITLE 'DOUGIE''S TEST COBOL PROGRAM'
000200 IDENTIFICATION DIVISION.
000300   PROGRAM-ID. DGL.
000400 ENVIRONMENT DIVISION.
000500 INPUT-OUTPUT SECTION.
000600 FILE-CONTROL.
000700       SELECT INPUT-FILE
000800         ASSIGN TO INFILE
000900         ORGANIZATION IS SEQUENTIAL.
001000 DATA DIVISION.
001100 FILE SECTION.
001200 FD   INPUT-FILE
001300      BLOCK 0 RECORDS
001400*     RECORD 80 CHARACTERS
001500      LABEL RECORDS OMITTED
001600      DATA RECORD IS INPUT-RECORD.
001700 01  INPUT-RECORD.
001800     05 IN-COMMAND PIC X.
001900     05 IN-NUMBER.
002000        10 IN-DIGIT-1 PIC 9.
002100        10 IN-DIGIT-2 PIC 9.
002200        10 IN-DIGIT-3 PIC 9.
002300     05 PIC X(76).
002400 WORKING-STORAGE SECTION.
002500 01  INPUT-FILE-EOF      PIC X(5).
002600     88  EOF-MET VALUE 'E-O-F'.
002700 01  FERRY-ARRAY.
002800     05 FERRY-ENTRY
002900        OCCURS 1000 TIMES INDEXED BY INDEX-NUM.
003000        10 FERRY-COMMAND PIC X.
003100        10 FERRY-NUMBER PIC 999.
003200 01  FERRY-POSITION.
003300     05 FERRY-NORTH PIC S9(6).
003400     05 FERRY-EAST PIC S9(6).
003500 01  WAYPOINT-POSITION.
003600     05 WAYPOINT-NORTH PIC S9(6).
003700     05 WAYPOINT-EAST PIC S9(6).
003800 77  MANHATTEN PIC 9(6).
003900 77  TEMP-TURNS PIC S999.
004000 77  TEMP-LOOP-COUNTER PIC 999.
004100 77  TEMP-WAYPOINT PIC S9(4).
004200
004300 PROCEDURE DIVISION.
004400 PROCEDURE-HEADER-000.
004500     OPEN INPUT INPUT-FILE
004600     SET INDEX-NUM TO 1
004700     PERFORM WITH TEST BEFORE UNTIL EOF-MET
004800       READ INPUT-FILE
004900       AT END
005000         MOVE "S" TO FERRY-COMMAND(INDEX-NUM)
005100         MOVE 0 TO FERRY-NUMBER(INDEX-NUM)
005200         SET EOF-MET TO TRUE
005300       END-READ
005400       IF NOT EOF-MET THEN
005500         MOVE IN-COMMAND TO FERRY-COMMAND(INDEX-NUM)
005600         COMPUTE FERRY-NUMBER(INDEX-NUM) = 0 +
005700                 IN-DIGIT-1
005800         IF IN-DIGIT-2 IS NUMERIC THEN
005900           COMPUTE FERRY-NUMBER(INDEX-NUM) =
006000                   (FERRY-NUMBER(INDEX-NUM) * 10) +
006100                   IN-DIGIT-2
006200         END-IF
006300         IF IN-DIGIT-3 IS NUMERIC THEN
006400           COMPUTE FERRY-NUMBER(INDEX-NUM) =
006500                   (FERRY-NUMBER(INDEX-NUM) * 10) +
006600                   IN-DIGIT-3
006700         END-IF
006800         SET INDEX-NUM UP BY 1
006900       END-IF
007000     END-PERFORM
007100     CLOSE INPUT-FILE
007200*
007300     MOVE 0 TO FERRY-NORTH
007400     MOVE 0 TO FERRY-EAST
007500     MOVE 10 TO WAYPOINT-EAST
007600     MOVE 1 TO WAYPOINT-NORTH
007700
007800     PERFORM VARYING INDEX-NUM FROM 1 BY 1
007900      UNTIL FERRY-COMMAND(INDEX-NUM) = "S" AND
008000            FERRY-NUMBER(INDEX-NUM) = 0
008100       EVALUATE FERRY-COMMAND(INDEX-NUM)
008200        WHEN "E"
008300         PERFORM GO-EAST
008400        WHEN "W"
008500         PERFORM GO-WEST
008600        WHEN "S"
008700         PERFORM GO-SOUTH
008800        WHEN "N"
008900         PERFORM GO-NORTH
009000        WHEN "F"
009100         PERFORM GO-FORTH
009200        WHEN "L"
009300         PERFORM TURN-LEFT
009400        WHEN "R"
009500         PERFORM TURN-RIGHT
009600       END-EVALUATE
009700     END-PERFORM
009710     IF FERRY-EAST < 0 THEN
009720       COMPUTE FERRY-EAST = 0 - FERRY-EAST
009730     END-IF
009740     IF FERRY-NORTH < 0 THEN
009750       COMPUTE FERRY-NORTH = 0 - FERRY-NORTH
009760     END-IF
009800     COMPUTE MANHATTEN = FERRY-EAST + FERRY-NORTH
009900     DISPLAY "Manhatten: ", MANHATTEN
010000*
010100     STOP RUN.
010200*
010300 GO-EAST.
010400     COMPUTE WAYPOINT-EAST =
010500             WAYPOINT-EAST + FERRY-NUMBER(INDEX-NUM)
010600*    DISPLAY "e we: ", WAYPOINT-EAST
010700     EXIT.
010800 GO-WEST.
010900     COMPUTE WAYPOINT-EAST =
011000             WAYPOINT-EAST - FERRY-NUMBER(INDEX-NUM)
011100*    DISPLAY "w we: ", WAYPOINT-EAST
011200     EXIT.
011300 GO-SOUTH.
011400     COMPUTE WAYPOINT-NORTH =
011500             WAYPOINT-NORTH - FERRY-NUMBER(INDEX-NUM)
011600*    DISPLAY "s wn: ", WAYPOINT-NORTH
011700     EXIT.
011800 GO-NORTH.
011900     COMPUTE WAYPOINT-NORTH =
012000             WAYPOINT-NORTH + FERRY-NUMBER(INDEX-NUM)
012100*    DISPLAY "n wn: ", WAYPOINT-NORTH
012200     EXIT.
012300 GO-FORTH.
012400     COMPUTE FERRY-NORTH =
012500             FERRY-NORTH +
012600             (WAYPOINT-NORTH * FERRY-NUMBER(INDEX-NUM))
012700     COMPUTE FERRY-EAST =
012800             FERRY-EAST +
012900             (WAYPOINT-EAST * FERRY-NUMBER(INDEX-NUM))
012910*    DISPLAY "f fe: ", FERRY-EAST
012920*            " fn: ", FERRY-NORTH
013000     EXIT.
013100 TURN-LEFT.
013200     COMPUTE TEMP-TURNS = FERRY-NUMBER(INDEX-NUM) / 90
013210*    DISPLAY "l turns: ", TEMP-TURNS
013300     PERFORM VARYING TEMP-LOOP-COUNTER
013400      FROM 0 BY 1
013500      UNTIL TEMP-LOOP-COUNTER = TEMP-TURNS
013600       MOVE WAYPOINT-NORTH TO TEMP-WAYPOINT
013700       MOVE WAYPOINT-EAST TO WAYPOINT-NORTH
013800       COMPUTE WAYPOINT-EAST =
013900               TEMP-WAYPOINT * -1
014000*      DISPLAY "l amt: ", FERRY-NUMBER(INDEX-NUM),
014100*              " we: ", WAYPOINT-EAST,
014200*              " wn: ", WAYPOINT-NORTH
014300     END-PERFORM
014400     EXIT.
014500 TURN-RIGHT.
014600     COMPUTE TEMP-TURNS = FERRY-NUMBER(INDEX-NUM) / 90
014610*    DISPLAY "r turns: ", TEMP-TURNS
014700     PERFORM VARYING TEMP-LOOP-COUNTER
014800      FROM 0 BY 1
014900      UNTIL TEMP-LOOP-COUNTER = TEMP-TURNS
015000       MOVE WAYPOINT-NORTH TO TEMP-WAYPOINT
015100       COMPUTE WAYPOINT-NORTH =
015200               WAYPOINT-EAST * -1
015300       MOVE TEMP-WAYPOINT TO WAYPOINT-EAST
015400*      DISPLAY "r amt: ", FERRY-NUMBER(INDEX-NUM),
015500*              " we: ", WAYPOINT-EAST,
015600*              " wn: ", WAYPOINT-NORTH
015700     END-PERFORM
015800     EXIT.
