000100 TITLE 'DOUGIE''S TEST COBOL PROGRAM'
000200 IDENTIFICATION DIVISION.
000300   PROGRAM-ID. DGL.
000400 ENVIRONMENT DIVISION.
000500 INPUT-OUTPUT SECTION.
000600 FILE-CONTROL.
000700       SELECT INPUT-FILE
000800         ASSIGN TO INFILE
000900         ORGANIZATION IS SEQUENTIAL.
001000 DATA DIVISION.
001100 FILE SECTION.
001200 FD   INPUT-FILE
001300      BLOCK 0 RECORDS
001400*     RECORD 80 CHARACTERS
001500      LABEL RECORDS OMITTED
001600      DATA RECORD IS INPUT-RECORD.
001700 01  INPUT-RECORD.
001800     05 IN-REC PIC X(120).
001900 WORKING-STORAGE SECTION.
002000 01  INPUT-FILE-EOF      PIC X(5).
002100     88  EOF-MET VALUE 'E-O-F'.
002200 01  INPUT-ARRAY.
002300     05 INPUT-ARRAY-1 PIC X(120)
002400     OCCURS 9 TIMES INDEXED BY INPUT-IDX.
002500 01  PASSPORT-ARRAY.
002600     05 PASSPORT-ARRAY-1
002700     OCCURS 296 TIMES INDEXED BY PASSPORT-IDX.
002800        10 PASSPORT-BYR PIC X(5).
002900        10 PASSPORT-IYR PIC X(5).
003000        10 PASSPORT-EYR PIC X(5).
003100        10 PASSPORT-HGT PIC X(6).
003200        10 PASSPORT-HCL PIC X(8).
003300        10 PASSPORT-ECL PIC X(8).
003400        10 PASSPORT-PID PIC X(10).
003500        10 PASSPORT-CID PIC X(4).
003600        10 PASSPORT-VALID PIC X(7).
003700 01  FIELD-ARRAY.
003800     05 FIELDS
003900        OCCURS 8 TIMES.
004000        10 FIELD-KEY PIC X(10).
004100        10 FIELD-VALUE PIC X(10).
004200 77  I-IDX PIC S9(4) COMP.
004300 77  F-IDX PIC S9(4) COMP.
004400 77  P-IDX PIC S9(4) COMP.
004500 77  INVALID-PASSPORTS PIC S9(4) COMP VALUE +0.
004600 77  VALID-PASSPORTS PIC S9(4) COMP VALUE +0.
004700 PROCEDURE DIVISION.
004800 PROCEDURE-HEADER-000.
004900     OPEN INPUT INPUT-FILE
005000     SET INPUT-IDX TO 1
005100     SET PASSPORT-IDX TO 1
005200     MOVE SPACES TO PASSPORT-ARRAY-1(PASSPORT-IDX)
005300     PERFORM WITH TEST BEFORE UNTIL EOF-MET
005400       READ INPUT-FILE
005500       AT END
005600         SET EOF-MET TO TRUE
005700* Last record isn't delimited by a line of spaces
005800         PERFORM PARSE-PASSPORT THRU PARSE-PASSPORT-EXIT
005900       END-READ
006000       IF NOT EOF-MET THEN
006100         IF IN-REC NOT = SPACES
006200           MOVE IN-REC TO INPUT-ARRAY-1(INPUT-IDX)
006300           IF INPUT-IDX <= 8 THEN
006400             SET INPUT-IDX UP BY 1
006500           ELSE
006600             SET INPUT-IDX TO 1
006700             MOVE SPACES TO INPUT-ARRAY
006800           END-IF
006900         ELSE
007000           PERFORM PARSE-PASSPORT THRU PARSE-PASSPORT-EXIT
007100           SET INPUT-IDX TO 1
007200           MOVE SPACES TO INPUT-ARRAY
007300           SET PASSPORT-IDX UP BY 1
007400           MOVE SPACES TO PASSPORT-ARRAY-1(PASSPORT-IDX)
007500         END-IF
007600       END-IF
007700     END-PERFORM
007800     CLOSE INPUT-FILE
007900     PERFORM CHECK-VALIDITY THRU CHECK-VALIDITY-EXIT
008000     DISPLAY "Valid passports: ", VALID-PASSPORTS
008100     DISPLAY "Invalid passports: ", INVALID-PASSPORTS
008200*
008300     STOP RUN.
008400 PARSE-PASSPORT.
008500     PERFORM VARYING I-IDX FROM 1 BY 1 UNTIL I-IDX > 8
008600       MOVE SPACES TO FIELD-ARRAY
008700       UNSTRING INPUT-ARRAY-1(I-IDX) DELIMITED BY SPACES OR ':'
008800        INTO FIELD-KEY(1),
008900             FIELD-VALUE(1),
009000             FIELD-KEY(2),
009100             FIELD-VALUE(2),
009200             FIELD-KEY(3),
009300             FIELD-VALUE(3),
009400             FIELD-KEY(4),
009500             FIELD-VALUE(4),
009600             FIELD-KEY(5),
009700             FIELD-VALUE(5),
009800             FIELD-KEY(6),
009900             FIELD-VALUE(6),
010000             FIELD-KEY(7),
010100             FIELD-VALUE(7),
010200             FIELD-KEY(8),
010300             FIELD-VALUE(8)
010400       PERFORM VARYING F-IDX FROM 1 BY 1 UNTIL F-IDX > 8
010500         IF FIELD-KEY(F-IDX) NOT = SPACES THEN
010600           PERFORM PARSE-FIELD THRU PARSE-FIELD-EXIT
010700         END-IF
010800       END-PERFORM
010900     END-PERFORM
011000     MOVE SPACES TO INPUT-ARRAY
011100     .
011200 PARSE-PASSPORT-EXIT. EXIT.
011300 PARSE-FIELD.
011400     EVALUATE FIELD-KEY(F-IDX)
011500      WHEN "byr"
011600       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-BYR(PASSPORT-IDX)
011700      WHEN "iyr"
011800       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-IYR(PASSPORT-IDX)
011900      WHEN "eyr"
012000       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-EYR(PASSPORT-IDX)
012100      WHEN "hgt"
012200       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-HGT(PASSPORT-IDX)
012300      WHEN "hcl"
012400       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-HCL(PASSPORT-IDX)
012500      WHEN "ecl"
012600       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-ECL(PASSPORT-IDX)
012700      WHEN "pid"
012800       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-PID(PASSPORT-IDX)
012900      WHEN "cid"
013000       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-CID(PASSPORT-IDX)
013100      WHEN OTHER
013200       CONTINUE
013300     .
013400 PARSE-FIELD-EXIT. EXIT.
013500 CHECK-VALIDITY.
013600     PERFORM VARYING P-IDX FROM 1 BY 1 UNTIL P-IDX >= 296
013700       IF PASSPORT-BYR(P-IDX) = SPACES OR
014100          PASSPORT-IYR(P-IDX) = SPACES OR
014600          PASSPORT-EYR(P-IDX) = SPACES OR
015100          PASSPORT-HGT(P-IDX) = SPACES OR
015600          PASSPORT-HCL(P-IDX) = SPACES OR
016100          PASSPORT-ECL(P-IDX) = SPACES OR
016600          PASSPORT-PID(P-IDX) = SPACES THEN
016800         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
016900         ADD 1 TO INVALID-PASSPORTS
017000       ELSE
017010         MOVE " VALID " TO PASSPORT-VALID(P-IDX)
017020         ADD 1 TO VALID-PASSPORTS
017030       END-IF
017100       DISPLAY P-IDX, " ", PASSPORT-ARRAY-1(P-IDX)
017200     END-PERFORM
017300     .
017400 CHECK-VALIDITY-EXIT. EXIT.
