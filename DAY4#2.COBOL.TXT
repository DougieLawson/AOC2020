000100 TITLE 'DOUGIE''S TEST COBOL PROGRAM'
000200 IDENTIFICATION DIVISION.
000300   PROGRAM-ID. DGL.
000400 ENVIRONMENT DIVISION.
000500 INPUT-OUTPUT SECTION.
000600 FILE-CONTROL.
000700       SELECT INPUT-FILE
000800         ASSIGN TO INFILE
000900         ORGANIZATION IS SEQUENTIAL.
001000 DATA DIVISION.
001100 FILE SECTION.
001200 FD   INPUT-FILE
001300      BLOCK 0 RECORDS
001400*     RECORD 80 CHARACTERS
001500      LABEL RECORDS OMITTED
001600      DATA RECORD IS INPUT-RECORD.
001700 01  INPUT-RECORD.
001800     05 IN-REC PIC X(120).
001900 WORKING-STORAGE SECTION.
002000 01  INPUT-FILE-EOF      PIC X(5).
002100     88  EOF-MET VALUE 'E-O-F'.
002200 01  INPUT-ARRAY.
002300     05 INPUT-ARRAY-1 PIC X(120)
002400     OCCURS 9 TIMES INDEXED BY INPUT-IDX.
002500 01  PASSPORT-ARRAY.
002600     05 PASSPORT-ARRAY-1
002700     OCCURS 296 TIMES INDEXED BY PASSPORT-IDX.
002800        10 PASSPORT-BYR PIC X(4).
002900        10 PASSPORT-IYR PIC X(4).
003000        10 PASSPORT-EYR PIC X(4).
003100        10 PASSPORT-HGT PIC X(6).
003200        10 PASSPORT-HCL PIC X(8).
003300        10 PASSPORT-ECL PIC X(8).
003400        10 PASSPORT-PID PIC X(11).
003500        10 PASSPORT-PIDN
003600           REDEFINES PASSPORT-PID
003700           PIC 9(9).
003800        10 PASSPORT-CID PIC X(4).
003900        10 PASSPORT-VALID PIC X(7).
004000 01  FIELD-ARRAY.
004100     05 FIELDS
004200        OCCURS 8 TIMES.
004300        10 FIELD-KEY PIC X(10).
004400        10 FIELD-VALUE PIC X(10).
004500 77  I-IDX PIC S9(4) COMP.
004600 77  F-IDX PIC S9(4) COMP.
004700 77  P-IDX PIC S9(4) COMP.
004800 77  TEST-BYR PIC 9(4) COMP.
004900 77  TEST-IYR PIC 9(4) COMP.
005000 77  TEST-EYR PIC 9(4) COMP.
005100 77  TEST-PID PIC X(15).
005200 77  TEST-PID-COUNT PIC S9(4) COMP.
005300 77  TEST-IN PIC 9(3) COMP.
005400 77  TEST-CM PIC 9(4) COMP.
005500 77  INVALID-PASSPORTS PIC S9(4) COMP VALUE +0.
005600 77  VALID-PASSPORTS PIC S9(4) COMP VALUE +295.
005700 PROCEDURE DIVISION.
005800 PROCEDURE-HEADER-000.
005900     OPEN INPUT INPUT-FILE
006000     SET INPUT-IDX TO 1
006100     SET PASSPORT-IDX TO 1
006200     MOVE SPACES TO PASSPORT-ARRAY-1(PASSPORT-IDX)
006300     PERFORM WITH TEST BEFORE UNTIL EOF-MET
006400       READ INPUT-FILE
006500       AT END
006600         SET EOF-MET TO TRUE
006700* Last record isn't delimited by a line of spaces
006800         PERFORM PARSE-PASSPORT THRU PARSE-PASSPORT-EXIT
006900       END-READ
007000       IF NOT EOF-MET THEN
007100         IF IN-REC NOT = SPACES
007200           MOVE IN-REC TO INPUT-ARRAY-1(INPUT-IDX)
007300           IF INPUT-IDX <= 8 THEN
007400             SET INPUT-IDX UP BY 1
007500           ELSE
007600             SET INPUT-IDX TO 1
007700             MOVE SPACES TO INPUT-ARRAY
007800           END-IF
007900         ELSE
008000           PERFORM PARSE-PASSPORT THRU PARSE-PASSPORT-EXIT
008100           SET INPUT-IDX TO 1
008200           MOVE SPACES TO INPUT-ARRAY
008300           SET PASSPORT-IDX UP BY 1
008400           MOVE SPACES TO PASSPORT-ARRAY-1(PASSPORT-IDX)
008500         END-IF
008600       END-IF
008700     END-PERFORM
008800     CLOSE INPUT-FILE
008900     PERFORM CHECK-VALIDITY THRU CHECK-VALIDITY-EXIT
009000     DISPLAY "Valid passports: ", VALID-PASSPORTS
009100     DISPLAY "Invalid passports: ", INVALID-PASSPORTS
009200*
009300     STOP RUN.
009400 PARSE-PASSPORT.
009500     PERFORM VARYING I-IDX FROM 1 BY 1 UNTIL I-IDX > 8
009600       MOVE SPACES TO FIELD-ARRAY
009700       UNSTRING INPUT-ARRAY-1(I-IDX) DELIMITED BY SPACES OR ':'
009800        INTO FIELD-KEY(1),
009900             FIELD-VALUE(1),
010000             FIELD-KEY(2),
010100             FIELD-VALUE(2),
010200             FIELD-KEY(3),
010300             FIELD-VALUE(3),
010400             FIELD-KEY(4),
010500             FIELD-VALUE(4),
010600             FIELD-KEY(5),
010700             FIELD-VALUE(5),
010800             FIELD-KEY(6),
010900             FIELD-VALUE(6),
011000             FIELD-KEY(7),
011100             FIELD-VALUE(7),
011200             FIELD-KEY(8),
011300             FIELD-VALUE(8)
011400       PERFORM VARYING F-IDX FROM 1 BY 1 UNTIL F-IDX > 8
011500         IF FIELD-KEY(F-IDX) NOT = SPACES THEN
011600           PERFORM PARSE-FIELD THRU PARSE-FIELD-EXIT
011700         END-IF
011800       END-PERFORM
011900     END-PERFORM
012000     MOVE SPACES TO INPUT-ARRAY
012100     .
012200 PARSE-PASSPORT-EXIT. EXIT.
012300 PARSE-FIELD.
012400     EVALUATE FIELD-KEY(F-IDX)
012500      WHEN "byr"
012600       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-BYR(PASSPORT-IDX)
012700      WHEN "iyr"
012800       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-IYR(PASSPORT-IDX)
012900      WHEN "eyr"
013000       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-EYR(PASSPORT-IDX)
013100      WHEN "hgt"
013200       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-HGT(PASSPORT-IDX)
013300      WHEN "hcl"
013400       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-HCL(PASSPORT-IDX)
013500      WHEN "ecl"
013600       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-ECL(PASSPORT-IDX)
013700      WHEN "pid"
013800       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-PID(PASSPORT-IDX)
013900      WHEN "cid"
014000       MOVE FIELD-VALUE(F-IDX) TO PASSPORT-CID(PASSPORT-IDX)
014100      WHEN OTHER
014200       CONTINUE
014300     .
014400 PARSE-FIELD-EXIT. EXIT.
014500 CHECK-VALIDITY.
014600     PERFORM VARYING P-IDX FROM 1 BY 1 UNTIL P-IDX >= 296
014700       IF PASSPORT-BYR(P-IDX) = SPACES OR
014800          PASSPORT-IYR(P-IDX) = SPACES OR
014900          PASSPORT-EYR(P-IDX) = SPACES OR
015000          PASSPORT-HGT(P-IDX) = SPACES OR
015100          PASSPORT-HCL(P-IDX) = SPACES OR
015200          PASSPORT-ECL(P-IDX) = SPACES OR
015300          PASSPORT-PID(P-IDX) = SPACES THEN
015400         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
015500         ADD 1 TO INVALID-PASSPORTS
015600         SUBTRACT 1 FROM VALID-PASSPORTS
015700       ELSE
015800         PERFORM PASSPORT-FIELD THRU PASSPORT-FIELD-EXIT
015900         IF PASSPORT-VALID(P-IDX) = "INVALID" THEN
016000           ADD 1 TO INVALID-PASSPORTS
016100           SUBTRACT 1 FROM VALID-PASSPORTS
016200         ELSE
016300           DISPLAY P-IDX, " ", PASSPORT-ARRAY-1(P-IDX)
016400         END-IF
016500       END-IF
016600     END-PERFORM
016700     .
016800 CHECK-VALIDITY-EXIT. EXIT.
016900 PASSPORT-FIELD.
017000     MOVE PASSPORT-BYR(P-IDX) TO TEST-BYR
017100     MOVE PASSPORT-IYR(P-IDX) TO TEST-IYR
017200     MOVE PASSPORT-EYR(P-IDX) TO TEST-EYR
017300*
017400     IF TEST-BYR < 1920 OR
017500        TEST-BYR > 2002 THEN
017600         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
017700     END-IF
017800*
017900     IF TEST-IYR < 2010 OR
018000        TEST-IYR > 2020 THEN
018100         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
018200     END-IF
018300*
018400     IF TEST-EYR < 2020 OR
018500        TEST-EYR > 2030 THEN
018600         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
018700     END-IF
018800*
018900     EVALUATE PASSPORT-ECL(P-IDX)
019000      WHEN "amb"
019100       CONTINUE
019200      WHEN "blu"
019300       CONTINUE
019400      WHEN "brn"
019500       CONTINUE
019600      WHEN "gry"
019700       CONTINUE
019800      WHEN "grn"
019900       CONTINUE
020000      WHEN "hzl"
020100       CONTINUE
020200      WHEN "oth"
020300       CONTINUE
020400      WHEN OTHER
020500         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
020600     END-EVALUATE
020700*
020800     IF LENGTH OF PASSPORT-HCL(P-IDX) < 7 OR
020900        PASSPORT-HCL(P-IDX)(1:1) NOT = "#" THEN
021000       MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
021100     END-IF
021200*
021300     MOVE 0 TO TEST-PID
021400     UNSTRING
021500       PASSPORT-PID(P-IDX)
021600       DELIMITED BY ALL SPACES
021700       INTO TEST-PID
021800       COUNT IN TEST-PID-COUNT
021900     END-UNSTRING
022000     IF TEST-PID-COUNT NOT = 9 OR
022100        PASSPORT-PIDN(P-IDX) IS NOT NUMERIC
022200       MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
022300     END-IF
022400*
022500     IF PASSPORT-HGT(P-IDX)(3:2) = "in" THEN
022600       MOVE PASSPORT-HGT(P-IDX)(1:2) TO TEST-IN
022700       IF TEST-IN < 59 OR
022800          TEST-IN > 76 THEN
022900         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
023000*
023100       END-IF
023200     ELSE
023300       IF PASSPORT-HGT(P-IDX)(4:2) = "cm" THEN
023400         MOVE PASSPORT-HGT(P-IDX)(1:3) TO TEST-CM
023500         IF TEST-CM < 150 OR
023600            TEST-CM > 193 THEN
023700           MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
023800*
023900         END-IF
024000       ELSE
024100         MOVE "INVALID" TO PASSPORT-VALID(P-IDX)
024200*
024300       END-IF
024400     END-IF
024500     .
024600 PASSPORT-FIELD-EXIT. EXIT.
