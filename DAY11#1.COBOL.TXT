000100 TITLE 'DOUGIE''S TEST COBOL PROGRAM'
000200 IDENTIFICATION DIVISION.
000300   PROGRAM-ID. DGL.
000400 ENVIRONMENT DIVISION.
000500 INPUT-OUTPUT SECTION.
000600 FILE-CONTROL.
000700       SELECT INPUT-FILE
000800         ASSIGN TO INFILE
000900         ORGANIZATION IS SEQUENTIAL.
001000 DATA DIVISION.
001100 FILE SECTION.
001200 FD   INPUT-FILE
001300      BLOCK 0 RECORDS
001400*     RECORD 80 CHARACTERS
001500      LABEL RECORDS OMITTED
001600      DATA RECORD IS INPUT-RECORD.
001700 01  INPUT-RECORD.
001800     05 IN-SEAT-ROW.
001900        10 IN-SEAT
002000           OCCURS 120 TIMES
002100           PIC X.
002200 WORKING-STORAGE SECTION.
002300 01 INPUT-FILE-EOF      PIC X(5).
002400    88  EOF-MET VALUE 'E-O-F'.
002500 01 SEATING-ARRAY.
002600    05 SEAT-ROWS
002700       OCCURS 100 TIMES.
002800       10 PIC X VALUE ".".
002900       10 SEAT-ROW PIC X(120).
003000       10 SEAT
003100          REDEFINES SEAT-ROW
003200          OCCURS 120 TIMES
003300          PIC X.
003400 01 WORK-SEATING-ARRAY.
003500    05 WORK-SEAT-ROWS
003600       OCCURS 100 TIMES.
003700       10 PIC X VALUE ".".
003800       10 WORK-SEAT
003900          OCCURS 120 TIMES
004000          PIC X.
004100 01 NEARBY-ARRAY.
004200    05 NEARBY-OFFSETS
004300       OCCURS 8 TIMES.
004400       10 NEARBY-X PIC S9(4) COMP.
004500       10 NEARBY-Y PIC S9(4) COMP.
004600 01 WORK-ARRAY.
004700    05 WORK-NEARBY-OFFSETS
004800       OCCURS 8 TIMES.
004900       10 WORK-NEARBY-X PIC S9(4) COMP.
005000       10 WORK-NEARBY-Y PIC S9(4) COMP.
005100 77 TEMP-SEAT PIC X.
005200 77 TEST-NEARBY-X PIC S9(4) COMP.
005300 77 TEST-NEARBY-Y PIC S9(4) COMP.
005400 77 GENERATION-IDX PIC S999 COMP.
005500 77 SEAT-IDX-X PIC 9(4) COMP.
005600 77 SEAT-IDX-Y PIC 9(4) COMP.
005700 77 SEAT-ROW-LEN PIC S999 COMP.
005800 77 SEAT-ROW-SPACES PIC S999 COMP.
005900 77 TOTAL-ADJACENT PIC S99 COMP.
006000 77 TOTAL-OCCUPIED PIC S9(5) COMP.
006100 77 REC-COUNT PIC S999 COMP.
006200 77 NEARBY-IDX PIC 9 COMP.
006300 PROCEDURE DIVISION.
006400 PROCEDURE-HEADER-000.
006500*
006600*             |          |
006700*   -1, -1(1) | -1, 0(2) | -1, +1(3)
006800*             |          |
006900*   ----------+----------+----------
007000*             |          |
007100*    0, -1(4) |   HERE   |  0, +1(5)
007200*             |          |
007300*   ----------+----------+----------
007400*             |          |
007500*   +1, -1(6) | +1, 0(7) | +1, +1(8)
007600*             |          |
007700*
007800     MOVE -1 TO NEARBY-X(1)
007900     MOVE -1 TO NEARBY-Y(1)
008000
008100     MOVE -1 TO NEARBY-X(2)
008200     MOVE 0  TO NEARBY-Y(2)
008300
008400     MOVE -1 TO NEARBY-X(3)
008500     MOVE +1 TO NEARBY-Y(3)
008600
008700     MOVE 0  TO NEARBY-X(4)
008800     MOVE -1 TO NEARBY-Y(4)
008900
009000     MOVE 0  TO NEARBY-X(5)
009100     MOVE +1 TO NEARBY-Y(5)
009200
009300     MOVE +1 TO NEARBY-X(6)
009400     MOVE -1 TO NEARBY-Y(6)
009500
009600     MOVE +1 TO NEARBY-X(7)
009700     MOVE 0  TO NEARBY-Y(7)
009800
009900     MOVE +1 TO NEARBY-X(8)
010000     MOVE +1 TO NEARBY-Y(8)
010100
010200     OPEN INPUT INPUT-FILE
010300     MOVE 1 TO REC-COUNT
010400     MOVE "." TO SEAT-ROW(1)
010500     PERFORM WITH TEST BEFORE UNTIL EOF-MET
010600       READ INPUT-FILE
010700       AT END
010800         SET EOF-MET TO TRUE
010900       END-READ
011000       IF NOT EOF-MET THEN
011100         ADD 1 TO REC-COUNT
011200         MOVE IN-SEAT-ROW TO SEAT-ROW(REC-COUNT)
011300       END-IF
011400     END-PERFORM
011500     CLOSE INPUT-FILE
011600     INSPECT SEAT-ROW(2) TALLYING SEAT-ROW-SPACES FOR ALL SPACES
011700     COMPUTE SEAT-ROW-LEN = 122 - SEAT-ROW-SPACES
011800     DISPLAY "ROWS: ", REC-COUNT
011900     DISPLAY "COLS: ", SEAT-ROW-LEN
012000*
012100     PERFORM VARYING GENERATION-IDX FROM 1 BY 1
012200      UNTIL GENERATION-IDX > 500
012300       MOVE 0 TO TOTAL-OCCUPIED
012400       MOVE 2 TO SEAT-IDX-X
012500       PERFORM WITH TEST AFTER
012600        UNTIL SEAT-IDX-X > REC-COUNT
012700         MOVE 1 TO SEAT-IDX-Y
012800         PERFORM WITH TEST AFTER
012900          UNTIL SEAT-IDX-Y > SEAT-ROW-LEN
013000           MOVE SEAT(SEAT-IDX-X SEAT-IDX-Y) TO TEMP-SEAT
013100           IF SEAT(SEAT-IDX-X SEAT-IDX-Y) NOT = "." THEN
013200             PERFORM HOW-MANY-ADJACENT THRU
013300                     HOW-MANY-ADJACENT-EXIT
013400             IF SEAT(SEAT-IDX-X SEAT-IDX-Y) = "L" AND
013500                TOTAL-ADJACENT = 0 THEN
013600               MOVE "#" TO TEMP-SEAT
013700             ELSE
013800               IF SEAT(SEAT-IDX-X
013900                       SEAT-IDX-Y) = "#" AND
014000                  TOTAL-ADJACENT >= 4 THEN
014100                 MOVE "L" TO TEMP-SEAT
014200               END-IF
014300             END-IF
014400           END-IF
014500           MOVE TEMP-SEAT TO WORK-SEAT(SEAT-IDX-X SEAT-IDX-Y)
014600           IF WORK-SEAT(SEAT-IDX-X SEAT-IDX-Y) = "#" THEN
014700             ADD 1 TO TOTAL-OCCUPIED
014800           END-IF
014900           ADD 1 TO SEAT-IDX-Y
015000           MOVE 0 TO TOTAL-ADJACENT
015100         END-PERFORM
015200         ADD 1 TO SEAT-IDX-X
015300       END-PERFORM
015400       DISPLAY GENERATION-IDX, " TOTAL: ", TOTAL-OCCUPIED
015500       MOVE WORK-SEATING-ARRAY TO SEATING-ARRAY
015600     END-PERFORM
015700*
015800     STOP RUN.
015900*
016000 HOW-MANY-ADJACENT.
016100     PERFORM VARYING NEARBY-IDX FROM 1 BY 1
016200      UNTIL NEARBY-IDX > 8
016300       COMPUTE WORK-NEARBY-X(NEARBY-IDX) =
016400               NEARBY-X(NEARBY-IDX) + SEAT-IDX-X
016500       COMPUTE WORK-NEARBY-Y(NEARBY-IDX) =
016600               NEARBY-Y(NEARBY-IDX) + SEAT-IDX-Y
016700       IF WORK-NEARBY-X(NEARBY-IDX) >= 2 AND
016800          WORK-NEARBY-X(NEARBY-IDX) <= REC-COUNT AND
016900          WORK-NEARBY-Y(NEARBY-IDX) >= 1 AND
017000          WORK-NEARBY-Y(NEARBY-IDX) <= SEAT-ROW-LEN THEN
017100         MOVE WORK-NEARBY-X(NEARBY-IDX) TO TEST-NEARBY-X
017200         MOVE WORK-NEARBY-Y(NEARBY-IDX) TO TEST-NEARBY-Y
017300         IF SEAT(TEST-NEARBY-X
017400                 TEST-NEARBY-Y) = "#" THEN
017500           ADD +1 TO TOTAL-ADJACENT
017600         END-IF
017700       END-IF
017800     END-PERFORM
017900     .
018000 HOW-MANY-ADJACENT-EXIT. EXIT.
